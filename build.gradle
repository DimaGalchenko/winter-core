plugins {
    id 'java'
    id 'jacoco'
}

apply from: 'version.gradle'

group = 'com.codeus.winter'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

jacoco {
    toolVersion = "0.8.12"
    reportsDirectory = layout.buildDirectory.dir('customJacocoReportDir')
}

dependencies {
    // Reflection
    implementation "org.reflections:reflections:${versions.reflections}"

    // Apache Commons
    implementation "org.apache.commons:commons-lang3:${versions.commons_lang}"
    implementation "org.apache.commons:commons-collections4:${versions.commons_collections}"

    // Log4j
    implementation "org.apache.logging.log4j:log4j-core:${versions.log4j}"
    implementation "org.apache.logging.log4j:log4j-api:${versions.log4j}"

    // JUnit Jupiter
    testImplementation platform("org.junit:junit-bom:${versions.junit}")
    testImplementation "org.junit.jupiter:junit-jupiter"
}

tasks.withType(JavaCompile).configureEach {
    options.release = 21
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport // report is always generated after tests run
}

jacocoTestReport {
    dependsOn test // tests are required to run before generating the report
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.2
            }
        }
    }
}

check.dependsOn jacocoTestCoverageVerification

wrapper {
    gradleVersion '8.8'
}